/*
 *  Copyright 2014-present the original author or authors.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

// *********************************************************************************************************************
// Enable plugins
// *********************************************************************************************************************
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'

// *********************************************************************************************************************
// Project Properties
// *********************************************************************************************************************
// Standard project properties. These are used as default values by some assembly and release task, e.g. jar

project.description = 'Java 10 code examples'
project.group = 'com.neiljbrown.examples'
//project.name - Defined in settings.gradle to override the default used by Gradle which is based on project dir name
//project.version - Defined in gradle.properties to allow it to be overridden

// Set Java source & class versions to use when compiling. Needs to be defined after applying java plugin to take affect
project.sourceCompatibility = 10 
project.targetCompatibility = 10 

// Extra user-defined project properties
ext {
  // Cribbed from https://github.com/mindscratch/reactor-offline/blob/master/build.gradle
  javadocLinks = [
    "http://docs.oracle.com/javase/10/docs/api/"
  ] as String[]
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  // Compile-time dependencies

  // Test dependencies
  testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
  // Workaround for JUnit 5 bug. Suppress warnings output on compiling test classes reporting that classes (enums) that
  // JUnit 5 depends upon can't be found. See https://github.com/junit-team/junit5/issues/1065
  testCompileOnly("org.apiguardian:apiguardian-api:${apiguardianApiVersion}")

  testCompile("org.assertj:assertj-core:${assertjVersion}")

  // Runtime dependencies
  testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

// *********************************************************************************************************************
// Task configuration
// *********************************************************************************************************************

idea {
  project {
    jdkName = '10'
    languageLevel = '10'
  }
}

// *********************************************************************************************************************
// Tasks
// *********************************************************************************************************************

// Custom task for running javadoc on Java classes in src/test/java
task javadocTests(type: Javadoc) {
  source = sourceSets.test.allJava
  classpath = configurations.testCompile
  options.links(project.ext.javadocLinks)
}
